## readme_example.silica
## Jacob Peck
## 20140319

## This is the interactive example from the readme, as a script
## to run it, do a silica readme_example.silica

## lines that begin with # are comments, and skipped
## empty lines are skipped as well

# let's start by playing a note
play

# now let's play with pitches
rp play rp play lp play lp play

# let's do the same thing, but with a repetition factor
2rp+play 2lp+play

# let's play a half note
x2 play s2

# let's define a macro for playing a half note... "play long"
pl >> x2 play s2

# test it?
pl

# let's define a structured macro -- a 'group' if you will
# this is equivalent to 'hat >> begingroup play rp play lp play endgroup'
hat = play rp play lp play
hat

# now let's make a longer piece using hat
hat3 = hat rp hat lp hat
hat3

# now, a parameterized version of hat
hatx(x) = x rp x lp x

# now, play it with a few different parameters
hatx(play)
hatx(pl)
hatx(hat)
hatx(hat3)

# and finally, exit
-exit
